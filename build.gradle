plugins {
    id "java-library"
    id "maven-publish"
    id "net.kyori.blossom" version "1.3.1"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = "UTF-8"

group = project.maven_group
archivesBaseName = project.maven_name
version = project.maven_version

repositories {
    mavenCentral()
    maven {
        name = "Lenni0451"
        url "https://maven.lenni0451.net/releases"
    }
    maven {
        name = "Lenni0451 Snapshots"
        url "https://maven.lenni0451.net/snapshots"
    }
    maven {
        name = "OpenCollab Releases"
        url = "https://repo.opencollab.dev/maven-releases/"
    }
    maven {
        name = "OpenCollab Snapshots"
        url = "https://repo.opencollab.dev/maven-snapshots/"
    }
    maven {
        name = "ViaVersion"
        url "https://repo.viaversion.com"
    }
    maven {
        name = "Minecraft Libraries"
        url "https://libraries.minecraft.net"
    }
}

dependencies {
    compileOnly "com.viaversion:viaversion:4.7.1-SNAPSHOT"
    compileOnly("com.viaversion:viabackwards-common:4.7.1-SNAPSHOT") {
        exclude group: "com.viaversion", module: "viaversion"
        exclude group: "io.netty", module: "netty-all"
    }
    compileOnly "com.viaversion:viarewind-core:2.0.4-SNAPSHOT"
    compileOnly "net.raphimc:ViaLegacy:2.2.17"
    compileOnly "net.raphimc:ViaAprilFools:2.0.7"
    compileOnly "net.raphimc:ViaBedrock:0.0.2-SNAPSHOT"
    compileOnly "org.cloudburstmc.netty:netty-transport-raknet:1.0.0.CR1-SNAPSHOT"

    api "org.slf4j:slf4j-api:2.0.7"
    api "org.yaml:snakeyaml:2.0"
}

blossom {
    replaceToken("\${version}", project.version, "src/main/java/net/raphimc/vialoader/ViaLoader.java")
}

java {
    withSourcesJar()
    withJavadocJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    repositories {
        maven {
            name = "Via"
            url = "https://repo.viaversion.com/"

            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = project.maven_group
            artifactId = project.maven_name
            version = project.maven_version

            from components.java

            pom {
                name = "ViaLoader"
                description = "Implementation of a ViaVersion based protocol translator platform"
                url = "https://github.com/ViaVersion/ViaLoader"
                licenses {
                    license {
                        name = "GPL-3.0 License"
                        url = "https://github.com/ViaVersion/ViaLoader/blob/main/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "RK_01"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/ViaVersion/ViaLoader.git"
                    developerConnection = "scm:git:ssh://github.com/ViaVersion/ViaLoader.git"
                    url = "https://github.com/ViaVersion/ViaLoader.git"
                }
            }
        }
    }
}
